buildscript {

	ext {
		kotlinVersion = '1.3.72'
		springBootVersion = '2.3.0.RELEASE'
		cxfVersion = '3.3.7'
		oidcTokenSupportVersion = '0.2.9'
		logstashLogbackVersion = '6.4'
		pensjonInformasjonVersion = '9.4.10'
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
		classpath("gradle.plugin.org.openrepose:gradle-jaxb-plugin:2.5.0")
	}
}

plugins {
	id 'com.github.ben-manes.versions' version '0.28.0'
	id 'se.patrikerdes.use-latest-versions' version '0.2.14'
	id "org.owasp.dependencycheck" version "5.3.2.1"
	id "org.sonarqube" version "3.0"
	id 'jacoco'
	id 'com.adarshr.test-logger' version '2.0.0'
	id 'com.cosminpolifronie.gradle.plantuml' version '1.6.0'
}

assert JavaVersion.current().isJava11Compatible(): "Java 11 or newer is required"

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.openrepose.gradle.plugins.jaxb'

group = 'no.nav.eessi.pensjon'
version = '0.0.1-SNAPSHOT'

dependencies {

	// Spring-boot
	implementation("org.springframework.boot:spring-boot-starter-aop")
	implementation("org.apache.tomcat.embed:tomcat-embed-core:9.0.35")

	implementation("org.springframework.boot:spring-boot-starter-actuator")
	implementation("org.springframework.boot:spring-boot-actuator")

	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
	implementation 'org.springframework.kafka:spring-kafka'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude module: 'junit'
		exclude module: "mockito-core"
		exclude module: "junit-vintage-engine"
	}
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testCompileOnly('junit:junit:4.13') {
		because "spring-kafka-test needs it - but we should not use it in our code"
	}

	implementation("no.nav.eessi.pensjon:ep-security-sts:0.0.10")

	// Apache CXF
	implementation("org.apache.cxf:cxf-spring-boot-starter-jaxws:${cxfVersion}")
	implementation("org.apache.cxf:cxf-rt-ws-security:${cxfVersion}")
	implementation("com.sun.xml.ws:jaxws-ri:2.3.3")
	implementation("org.hibernate:hibernate-validator:6.1.5.Final")

	implementation("commons-io:commons-io:2.7")

	// Allows for variable expiration-date of each cache-entry
	implementation("net.jodah:expiringmap:0.5.9")

	//mock - test
	testImplementation("org.junit.jupiter:junit-jupiter:5.6.2")
	testImplementation("org.junit.jupiter:junit-jupiter-params:5.6.2")
	testImplementation('org.mockito:mockito-junit-jupiter:3.3.3')
	testImplementation("com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0")
	testImplementation("org.mock-server:mockserver-netty:5.10.0") {
		exclude module: 'junit'
	}
	testImplementation("org.mock-server:mockserver-client-java:5.10.0")
	testImplementation "io.mockk:mockk:1.10.0"

	// Architecture tests
	testImplementation 'com.tngtech.archunit:archunit:0.13.1'
	testImplementation 'com.tngtech.archunit:archunit-junit5:0.13.1'

	// Micrometer
	implementation("io.micrometer:micrometer-registry-prometheus")
	implementation("no.nav.eessi.pensjon:ep-metrics:0.4.0")

	// Personoppslag
	implementation("no.nav.tjenestespesifikasjoner:person-v3-tjenestespesifikasjon:1+")
	implementation("no.nav.eessi.pensjon:ep-personoppslag:5.1.1")

	// Logging
	implementation("net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}")
	configurations { all*.exclude group: 'commons-logging', module: 'commons-logging' }
	implementation group: 'org.slf4j', name: 'jcl-over-slf4j'
	implementation("no.nav.eessi.pensjon:ep-logging:0.0.17")
}

test {
	useJUnitPlatform()
}

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
}
compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "11"
	}
}

compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '11'
	}
}

repositories {
	mavenCentral()

	["maven-release","tjenestespesifikasjoner","ep-metrics", "ep-security-sts"].each { repo ->
		String token = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key")
		if (!token) throw new NullPointerException("Missing token, you have to set GITHUB_TOKEN or gpr.key, see README")
		maven {
			url = uri("https://maven.pkg.github.com/navikt/$repo")
			credentials {
				username = "token"
				password = token
			}
		}
	}
}

wrapper {
	distributionType = Wrapper.DistributionType.ALL
}

// FIXME - det er noe som tryner i bygget knyttet til genereringen av svg fra plantuml
//tasks.check.dependsOn tasks.named("plantUml")

// https://github.com/cosminpolifronie/gradle-plantuml-plugin
//plantUml {
//	render input: 'src/test/resources/components.puml', output: 'components.svg', format: 'svg', withMetadata: false
//}

// https://docs.gradle.org/current/userguide/jacoco_plugin.html
jacoco {
	toolVersion = "0.8.5"
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}

tasks.sonarqube.dependsOn jacocoTestReport

/* https://github.com/ben-manes/gradle-versions-plugin */
dependencyUpdates {
	rejectVersionIf {
		['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'pr']
				.any { qualifier -> it.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/}
	}
	revision = 'release'
}
